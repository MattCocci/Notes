{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# Global imports and style settings\n",
    "using PyPlot, Distributions, LaTeXStrings, PyCall\n",
    "plt.style[:use](\"mystyle.mplstyle\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "# Monte Carlo Methods"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Generating Random Variables\n",
    "\n",
    "This section answers the question \"How can we draw samples of univariate random variable $X$ with cumulative distribution function (CDF) $F(x)$?\".\n",
    "\n",
    "Throughout, we will assume that we have some efficient algorithm to generate uniform random variables, $U\\sim \\text{Unif}([0,1])$, and then build to more complex distributions from this starting point."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Inverse Transform Method"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Intuition\n",
    "\n",
    "Suppose we want to draw samples of some arbitrary random variable $X$ with CDF denoted $F(x)$. (A specific example of a $\\text{Beta}(\\alpha,\\beta)$ RV is plotted below, though the intuition will hold for any Random Variable.) \n",
    "\n",
    "Now by the definition of the CDF, for any draw $\\hat{x}$, the value $F(\\hat{x})$ must lie on [0,1].\n",
    "\n",
    "This suggests the following approach for sampling draws of $X$:\n",
    "* Throw independent $\\text{Unif}[0,1]$ RV's, denoted $\\hat{u}_i$, agfainst the $y$-axis\n",
    "* Map those draws to the corresponding unique $\\hat{x}_i$ draws on the $x$-axis.\n",
    "\n",
    "The arrows show specific examples and the order to proceed in.\n",
    "\n",
    "When the CDF is steep, a small change in a $\\hat{u}_i$ draw won't change the corresponding $\\hat{x}_i$ very much; therefore, we'll accumulate lots of mass clustered around such $\\hat{x}_i$ over many draws. The opposite holds when the CDF is flat. In this way, we map uniform draws (which are easy to draw from using fast algorithms) directly into draws that match other highly non-uniform distributions. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "α, β = 0.25, 0.25\n",
    "f, F = map((fcn) -> ((x) -> fcn(Beta(α,β), x)), [pdf, cdf])\n",
    "x = 0:0.001:1\n",
    "\n",
    "p = Array(Any,4)\n",
    "figure(figsize=(5,4))\n",
    "\n",
    "plot(x,F(x), alpha=0.7);\n",
    "lines_at = [0.2, 0.75]; arwsz = 0.03;\n",
    "draw_arrow = (x,y,dx,dy,sz) -> arrow(x, y, dx, dy, head_width=sz,head_length=sz)\n",
    "p[1:2] = map(x -> draw_arrow(0, F(x), x-arwsz, 0, arwsz), lines_at)\n",
    "p[3:4] = map(x -> draw_arrow(x, F(x), 0, -F(x)+arwsz, arwsz), lines_at)\n",
    "map(ph -> setp(ph, facecolor=\"black\", edgecolor=\"black\", alpha=0.7), p)\n",
    "ylabel(L\"$F(x)$\"); title(L\"Example CDF of $X_i \\sim $ Beta$(\\alpha,\\beta)$\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### The Math\n",
    "\n",
    "\n",
    "To see why this works, suppose $X$ is some random variable with invertible CDF $F(x)$. Next suppose that $U\\sim\\text{Unif}([0,1])$.\n",
    "\n",
    "If we set $X' = F^{-1}(U)$, then $X'$ will have the same distribution as $X$.\n",
    "\n",
    "_Proof_\n",
    "$$\n",
    "\\begin{align*}\n",
    "    \\text{CDF of $X'$} = P(X' < x) &= P(F^{-1}(U) < x) \\\\\n",
    "    &= P(U < F(x)) = F(x) = \\text{CDF of $X$}\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "When $X$ is discrete, this procedure can be modified by drawing $U$ as before and setting $X' = \\inf \\{x : F(x) > u\\}$."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Numerical Example\n",
    "\n",
    "Suppose we want to draw $X_i \\sim \\text{Exp}(\\lambda)$. This has pdf and cdf as follows:\n",
    "\n",
    "$$\n",
    "\\begin{align*}\n",
    "    f(x) = \\lambda e^{-\\lambda x} \\qquad F(x) &= 1 - e^{-\\lambda x} \\\\\n",
    "    \\Rightarrow \\quad F^{-1}(u) &= -\\frac{1}{\\lambda}\\ln(1-u)\n",
    "\\end{align*}\n",
    "$$\n",
    "\n",
    "The three panels below show the different steps:\n",
    "1. Simulate $N$ draws from a uniform. The histogram depicts one particular example.\n",
    "2. For $i=1,\\ldots,N$, set $\\hat{x}_i = F^{-1}(\\hat{u}_i)$. The resulting empirical CDF for the $\\hat{x}_i$ draws is plotted against the true analytical CDF for comparison. \n",
    "3. Plot the analytical pdf against the samples of $\\hat{x}_i$ generated by the inverse transform method. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "N = 100\n",
    "λ = 1\n",
    "f, F = map((fcn) -> ((x) -> fcn(Exponential(λ), x)), [pdf, cdf])\n",
    "Finv = (u) -> -(1/λ)*log(1 - u)\n",
    "\n",
    "u_draws = rand(N)\n",
    "x_draws = Finv(u_draws)\n",
    "support, counts = hist(x_draws, 0:0.01:5)\n",
    "\n",
    "figure(figsize=(11,3))\n",
    "subplot(131)\n",
    "plt.hist(u_draws, 20, normed=1), title(L\"$\\hat{u}_i$ draws\")\n",
    "\n",
    "subplot(132)\n",
    "plot(support[2:end], cumsum(counts)/N, alpha=0.7)\n",
    "plot(support, F(support))\n",
    "title(\"Inverse Transform\"); legend((\"Empirical CDF\",\"Analytical CDF\"), loc=\"lower right\")\n",
    "\n",
    "subplot(133)\n",
    "plt.hist(x_draws, normed=1)\n",
    "plot(support, f(support))\n",
    "legend((\"Analytical PDF\", \"Empirical Distribution\")); title(L\"$\\hat{x}_i$ draws\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Drawbacks and Limitations\n",
    "\n",
    "There's really only one: You need to be able to invert the CDF to get $F^{-1}(u)$ analytically. \n",
    "\n",
    "So in practice, this method only works for uniform (duh), exponential, Cauchy, Weibull, logistic, and discrete RVs. For more general distributions, we'll have to use other methods."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Acceptance-Rejection Method"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Intuition\n",
    "\n",
    "Acceptance-Rejection builds directly upon the Inverse Transform Method, but allows random sampling of *any* arbitrary univariate RV--not just those with invertible CDFs.\n",
    "\n",
    "To get the intuition, suppose that we want to draw samples of target random variable $X$ with pdf $f(x)$, but we cannot use the inverse transform method. Next suppose that we *can* draw random samples of some pseudo-random variable $X'$ with proposal distribution $g(x)$ that covers $f(x)$. (\"Pseudo\" because the density will not integrate to one, and we'll have to normalize at some point in the process. More on that later.)\n",
    "\n",
    "An example is plotted below of the target pdf $f(x)$ for $X\\sim N(0,1)$ and a covering proposal distribution $g(x)$ for $X'$. We then apply the following procedure:\n",
    "1. Draw $\\hat{x}'$ from the proposal distribution, $g(x)$.\n",
    "2. Accept $\\hat{x}'$ as a draw for $X$ with probability equal to $\\frac{f(x')}{g(x')}$.\n",
    "\n",
    "Said another way, we draw to match proposal distribution $g(x)$ because it's easy and tractable. But since that isn't quite the distribution we want, we'll randomly reject a fraction of draws so that we match our target $f(x)$ in the limit. When the proposal distribution $g(x)$ is very different from $f(x)$--i.e. $g(x)~>>~f(x)$--there will be a larger \"correction\" that manifests in a higher rejection rate. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "f = (x) -> pdf(Normal(0,1), x)\n",
    "α = 1.2sqrt(e/(2*π))\n",
    "g = (x) -> α*exp(-abs(x))\n",
    "x = -4:0.01:4\n",
    "\n",
    "figure(figsize=(5,3))\n",
    "map((fcn) -> plot(x, fcn(x)), [f, g])\n",
    "plot(-0.25*ones(2,1), [0, 1], color=\"black\", linestyle=\"--\", linewidth=1)\n",
    "legend((L\"Target PDF, $f(x)$\", L\"Proposal, $g(x)$\"),fontsize=10);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### The Math\n",
    "\n",
    "We want to generate samples of some random variable $X$ with PDF $f(x)$. \n",
    "\n",
    "To do so, we first find a covering proposal distribution $g(x)$ such that\n",
    "\n",
    "$$\n",
    "    \\forall x \\quad g(x) > f(x) \n",
    "    \\qquad \\text{and} \\qquad\n",
    "    G(x) = \\int^x_{-\\infty} g(x) \\; dx\n",
    "$$\n",
    "\n",
    "where $G(x)$ is such that\n",
    "* $G^{-1}(u)$ exists\n",
    "* $\\lim_{x\\rightarrow\\infty} G(x) = k < \\infty$\n",
    "\n",
    "Then we can generate draws of $X$ using the following algorithm:\n",
    "1. Draw $\\hat{u}$ from a $\\text{Unif}([0,1])$ distribution.\n",
    "2. Set $\\hat{x}' = G^{-1}(k\\cdot\\hat{u})$ where $k$ is the normalizing constant (defined above) so that $\\lim_{x\\rightarrow\\infty}\\frac{1}{k}G(x) =1$.\n",
    "3. Draw $\\hat{r}$ from a $U([0,1])$ distribution.\n",
    "4. Accept draw and set $x = \\hat{x}'$ if \n",
    "$$\n",
    "    0 \\leq \\hat{r} \\leq \\frac{f(x)}{g(x)}\n",
    "$$\n",
    "Reject otherwise.\n",
    "5. Repeat until you have accepted $N$ draws.\n",
    "\n",
    "Code to implement this scheme below."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "function AcceptReject(N, f, Ginv, k)\n",
    "    n = 0\n",
    "    proposed = Array(Float64,0)\n",
    "    xdraws   = Array(Float64,N)\n",
    "    while n < N\n",
    "        u,r = rand(2)\n",
    "        xp  = Ginv(k*u)\n",
    "        if r ≤ f(xp)/g(xp)\n",
    "            n += 1\n",
    "            xdraws[n] = xp\n",
    "        end\n",
    "        push!(proposed,xp)\n",
    "    end\n",
    "    return xdraws, proposed\n",
    "end;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Final Note:** Ideally, we choose $g(x)$ so that it matches $f(x)$ as closely as possible and minimizes\n",
    "\n",
    "$$\n",
    "    \\int^\\infty_{-\\infty} [g(x)-f(x)] dx\n",
    "$$\n",
    "\n",
    "subject to the constraint that still $g(x)>f(x)$ for all $x$ and $G(x)$ is invertible. \n",
    "\n",
    "This minimizes \"wasted area\" between $f(x)$ and $g(x)$ that corresponds to higher a higher rejection rate and wasted draws."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Numerical Example\n",
    "\n",
    "We'll use the example from the intuition section above where $X\\sim N(0,1)$ and the target PDF and proposal distribution are, respectively,\n",
    "\n",
    "$$\n",
    "    f(x) = \\frac{1}{\\sqrt{2\\pi}} e^{-\\frac{x^2}{2}}\n",
    "    \\qquad\n",
    "    g(x) = \\sqrt{\\frac{e}{2\\pi}} e^{-|x|}\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "# Define target and proposal distributions\n",
    "f     = (x) -> pdf(Normal(0,1), x)\n",
    "α     = sqrt(2e/π)\n",
    "g     = (x) -> (α/2)*exp(-abs(x))\n",
    "G     = (x) -> (α/2)*( exp(-abs(x))*(x ≤ 0) + 2*(x>0))\n",
    "Ginv  = (u) -> (u ≤ α/2)*(log(2u/α)) + (u > α/2)*(-log(2-2u/α))\n",
    "k     = α #Normalizing constant\n",
    "\n",
    "# Generate N draws via accept reject\n",
    "xdraws, proposed = AcceptReject(10000, f, Ginv, k)\n",
    "\n",
    "# Plot sample\n",
    "figure(figsize=(8.5,3));\n",
    "ax = Array(Any, 2)\n",
    "xl = -5:0.01:5; yl = 0:0.01:0.55;\n",
    "\n",
    "ax[1] = subplot(121)\n",
    "map(arg -> plt.hist(arg[1], 50, normed=1, alpha=0.8, histtype=\"stepfilled\", zorder=arg[2]), \n",
    "    [(xdraws, 2), (proposed, 1)])\n",
    "legend((\"Accepted\", \"Proposed\"));\n",
    "\n",
    "ax[2] = subplot(122)\n",
    "plt.hist(xdraws, 50, normed=1, alpha=0.8, histtype=\"stepfilled\")\n",
    "plot(xl, f(xl))\n",
    "legend((\"Accepted Draws\", L\"Target $f(x)$\"))\n",
    "\n",
    "map(x -> setp(x, xlim=(xl[1],xl[end]), ylim=(yl[1], yl[end])), ax);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.3.8",
   "language": "julia",
   "name": "julia-0.3"
  },
  "language_info": {
   "name": "julia",
   "version": "0.3.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
